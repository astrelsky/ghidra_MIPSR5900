
:bc0f Rel16 is prime=0x10 & copop=0x8 & cc=0 & Rel16 {
    tmp:1 = getCopCondition(0:1, 0:1); 
    delayslot(1); 
    if (tmp != 0) goto inst_next; 
    goto Rel16; 
}
:bc0fl Rel16 is prime=0x10 & copop=0x8 & cc=2 & Rel16 {
    tmp:1 = getCopCondition(0:1, 0:1);  
    if (tmp != 0) goto inst_next; 
    delayslot(1);
    goto Rel16; 
}
:bc0t Rel16 is prime=0x10 & copop=0x8 & cc=1 & Rel16 {
    tmp:1 = getCopCondition(0:1, 0:1); 
    delayslot(1); 
    if (tmp == 0) goto inst_next; 
    goto Rel16; 
}
:bc0tl Rel16 is prime=0x10 & copop=0x8 & cc=3 & Rel16 {
    tmp:1 = getCopCondition(0:1, 0:1);  
    if (tmp == 0) goto inst_next; 
    delayslot(1);
    goto Rel16; 
}
# 0100 0000 000t tttt dddd d000 0000 0sss
:mfc0 RT, RD0                   is prime=16 & copop=0 & RT & RD0 & zero6=0 { 
    RT = sext( RD0:$(SIZETO4) );
}
# 1011 11bb bbbo oooo iiii iiii iiii iiii
:cache op, OFF_BASER6           is ((prime=47 & REL6=0) | (prime=0x1E & fct=0x25 & bit6=0)) & OFF_BASER6 & op {  
    cacheOp(op:1, OFF_BASER6); 
}
:ei is prime=0x10 & copop=0x10 & fct=0x38 { enableInterrupts(); }
:di is prime=0x10 & copop=0x10 & fct=0x39 { disableInterrupts(); }
# 0100 0010 0000 0000 0000 0000 0001 1000
:eret                           is prime=16 & fct=24 & bit25=1 & copfill=0  {
    return[EPC];
}
# 0100 0000 100t tttt dddd d000 0000 0sss
:mtc0 RTsrc, RD0, sel               is prime=16 & copop=4 & RTsrc & RD0 & zero6=0 & sel { 
	setCopReg(0:1, RD0, RTsrc, sel:1); 
}
:tlbp		is prime=0x10  & bit25=1 & copfill=0x00 & fct=0x08 {
    Index = TLB_probe_for_matching_entry(EntryHi);
}
:tlbr           is prime=0x10  & bit25=1 & copfill=0x00 & fct=0x01 {
    EntryHi = TLB_read_indexed_entryHi(Index);
    EntryLo0 = TLB_read_indexed_entryLo0(Index);
    EntryLo1 = TLB_read_indexed_entryLo1(Index);
    PageMask = TLB_read_indexed_entryLo1(Index);
}
:tlbwi		is prime=0x10  & bit25=1 & copfill=0x00 & fct=0x02 {
    TLB_write_indexed_entry(Index, EntryHi, EntryLo0, EntryLo1, PageMask);
}
:tlbwr          is prime=0x10  & bit25=1 & copfill=0x00 & fct=0x06 {
    TLB_write_random_entry(Random, EntryHi, EntryLo0, EntryLo1, PageMask);
}